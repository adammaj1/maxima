/*

 b batch file for maxima



There are 2 complex planes :
* w-plane 
* z-plane 


1 step transformation from w plane go to z plane using z=h(w)


 
*/

kill(all);
remvalue(all);


/*

*/



iMax:1000; /* number of points to draw */
wxMin: -80;
wxMax: 2;
wxStep: (wxMax-wxMin)/iMax;
k:0.2;
d:6;


/* =================== functions ============ */


/* computes w from wx 
GCarleson , Gamelin: Complex Dynamics, Springer, 1993, page 36

Julia Sets of Complex Polynomials and Their Implementation on the Computer by Christoph Martin Stroh
page 27 
*/

f(wx):= - k*wx +d;
h(w):=-1/w;


/* converts complex number into list for draw package  */
 draw_format(z):= [float(realpart(z)),float(imagpart(z))];



compile(all);



/* ============== compute ===============  */

/* point to point method of drawing 
 compute first point of curve, create list and save point to this list */

 
 wList:[]; 
 

 for wx:wxMin step wxStep  while (wx< wxMax+wxStep) do
  (  
     wy:f(wx),
     wList:cons(wx+wy*%i,wList),  
     wList:endcons(wx-wy*%i,wList)
 )$

 zList:map(h,wList)$

/* convert list to draw format */

 wList:map(draw_format,wList)$ 
 zList:map(draw_format,zList)$ 

/* =================  draw ======================================*/

path:"~/maxima/batch/julia/parabolic/z2z/stroh/"$ /* pwd  */
 FileName:concat(string(k),"d", string(d))$ /* without extension which is the terminal name */

 load(draw); /* Mario RodrÃ­guez Riotorto   http://www.telefonica.net/web2/biomates/maxima/gpdraw/index.html */
 draw(
  terminal  = 'svg,
  file_name = concat(path,FileName),
  columns  = 2,
  dimensions=[1000,500], /*  x = y*columns  */
  
  gr2d(title = " w plane ",
   /* xrange = [0,3],  */
   yrange = [-20.0,20.0],
   xrange = [-20.0,20.0],
   xtics={-10, -2, 0,2},
   grid = true,
   xaxis       = true,
   xaxis_type  = solid,
   points_joined =true,
   color         = green,
   point_size    = 0.5,
   point_type = filled_circle,
   points(wList)
   ),

  gr2d(title = " z plane",
   yrange = [-0.7,0.7],
   xrange = [-0.7,0.7],
   points_joined =true,
   color         = red,
   point_size    = 0.5,
   point_type = filled_circle,
   points(zList))

 );
