kill(all);
remvalue(all);










/* approiximated function using roots on the real axis*/
 GiveC( x):=block
( /* http://zunzun.com*/
  
  a : 0.53226927610784935,
  b : 0.65410208763684241,
  c : -1.4312869957125389,
  d : 0.84710834303177074,
  return (c*atan(exp((x-a)/b)) + d)
);





/* real quadratic  diagram based on the code by Mario Rodriguez Riotorto using Maxima CAS draw packag  */



pts:[]; /* points of the orbit */

ls:[];
kMax1:1000;
kMax2:500;

/* ------------- compute -----------------------------------*/

for ix:0 thru 10 step 0.1 do  
    (
       c:GiveC(ix),
       z: 0.0,
       l:0, /* Lyapunov exponent */
       for k:1 thru kMax1 do 
         (    
           z: z * z+c, /* to remove points from image compute and do not draw it */
           l:l+log(abs(2*z))
         ),
       
        for k:1 thru kMax2  do 
          ( 
           z:  z * z+c, /* compute and draw it */
           pts: cons([c,z], pts),/* save points to draw it later */
	   l:l+log(abs(2*z))	
          ),
       ls:cons([c,l/(kMax1+kMax2)],ls)   /* save points to draw it later */         
                   
    ); 


/* =================  draw ======================================*/

path:"~/maxima/batch/lyapunov/log/"$ /* pwd  */
 FileName:"l"$ /* without extension which is the terminal name */

load(draw);

draw(
  terminal  = 'svg,
  file_name = concat(path,FileName),
  
  dimensions=[1900,2600], /*  y = x*rows  */
  
  gr2d(points_joined =false,
   title      = "Bifurcation diagram, z[i+1] = z[i]*z[i] +c",
        xlabel     = "c parameter",
        ylabel     = "z ",
        ytics = 1.0,
        
        point_type = filled_circle,
        point_size = 0.2,
        color = black,
        points(pts) 
   ),

  gr2d(title = " Lyapunov exponent ",
   points_joined =true,
    yaxis       = true,
   xaxis 	= true,
    xlabel     = "c parameter",
   ylabel     = " lyapunov exponent ",
   yrange = [-4,1],
   color         = red,
   point_size    = 0.5,
   point_type = filled_circle,
   points(ls))

 );

