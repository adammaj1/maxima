 /* 
 batch file for maxima cas

 conformal mapping from :
 the  circle with center=0 and radius=1/2
 given by equation : abs(2*z)=1
 where : z:x+y*%i=r*cos(t)+%i*r*sin(t) = 
 onto 
 cardioid ( boundary of main hyperbolic component of Mandelbrot set
 is given by equation:
 c:w-w*w;
 based on :Conformal Mappings And The Area Of The Mandelbrot Set by David Allingham page 18
 http://www.eng.warwick.ac.uk/staff/doa/reports/allingham-thesis1995.pdf
 */ 


 /* conformal map */
 f(w):=w-w*w;
 iMax:100; /* number of points to draw */
 dt:1/iMax;

 /* 
 circle D={w:abs(2w)=1 } where w=l(t) 
 t is angle in turns ; 1 turn = 360 degree = 2*Pi radians 
 */
 l(t):=%e^(%i*t*2*%pi)/2;


 /* point to point method of drawing */
 t:0; /* angle in turns */ 


 /* compute first point of curve, create list and save point to this list */
 /* point of unit circle   w:l(t); */
 w:rectform(ev(l(t), numer)); 

 circleList:makelist (w, j, 1, 1); 

 for i:1 thru iMax step 1 do
  block
  (t:t+dt,
   w:rectform(ev(l(t), numer)), 
   circleList:cons(w,circleList)
  );

 /* convert one list into 2 lists */
 xxCircle:map(realpart, circleList);
 yyCircle:map(imagpart, circleList); 

 /* conformal mapping from  circle to cardioid */
 cardioidList: map(f, circleList);	

 /* convert one list into 2 lists */
 xxCardioid:map(realpart, cardioidList);
 yyCardioid:map(imagpart, cardioidList);


 load(draw); 
/* 
  draw package by Mario Rodr√≠guez Riotorto   
  http://riotorto.users.sourceforge.net/gnuplot/index.html 
  and enhaced text :
  http://riotorto.users.sourceforge.net/gnuplot/ps/index.html
*/

 draw(file_name = "a",
  pic_width=1000, 
  pic_height= 500,
  terminal  = 'svg,
  columns  = 2,
  gr2d(title = " circle D=\\{w:abs(2*w)=1 \\} ",
   points_joined =true,
   color         = red,
   point_type = 0,
   points(xxCircle,yyCircle)),
  gr2d(title      = "cardioid \\{c: c = w - w*w \\ } ",
   points_joined =true,
   color         = blue,
   point_type = 0,
   points(xxCardioid,yyCardioid)) 
 );
