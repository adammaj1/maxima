kill(all);
remvalue(all);


/* real quadratic  diagram based on the code by Mario Rodriguez Riotorto using Maxima CAS draw packag  */



pts:[]; /* points of the orbit */

ls:[];
kMax1:2000;
kMax2:1000;




roots:[];


iMax:1000;
c:0.25;
dc:(c+1.42)/iMax;
roots:cons([c,0],roots); /* first root */
checkP : false;

/* ------------- compute -----------------------------------*/

for  i:0 thru iMax  do 
    (
       
       z: 0.0,
       l:0, /* Lyapunov exponent */
       /* old l for finding roots */
       for k:1 thru kMax1  /*while (abs(z)<2)*/ do 
         (    
           z: z * z+c, /* to remove points from image compute and do not draw it */
           l:l+log(abs(2*z))
         ),
       
        for k:1 thru kMax2 /*while (abs(z)<2)*/ do 
          ( 
           z:  z * z+c, /* compute and draw it */
           pts: cons([c,z], pts),/* save points to draw it later */
	   l:l+log(abs(2*z))	
          ),
       l:l/(kMax1+kMax2),
       ls:cons([c,l],ls),   /* save points to draw it later */         
       if (l<lOld) 
         then ( if (checkP and c>-1.4011551890) then (checkP:false,roots:cons([c,0],roots)))   /*  found root ; desc:cons([c,0],desc)*/
         else checkP:true, /*   asc:cons([c,0],asc),    */
       lOld:l,
       c:c-dc
                     
    ); 
  tic:map(first,roots);
  tic:setify(tic);
  

/* =================  draw ======================================*/

path:"~/maxima/batch/realroots/maxima/"$ /* pwd  */
FileName:"r5"$ /* without extension which is the terminal name */

load(draw);

draw(
  terminal  = 'svg,
  file_name = concat(path,FileName),
  
  dimensions=[1900,2600], /*  y = x*rows  */
  
  gr2d(points_joined =false,
   title      = "Bifurcation diagram, z[i+1] = z[i]*z[i] +c",
        xlabel     = "c parameter",
        ylabel     = "z ",
        xtics = tic,
        point_type = filled_circle,
        point_size = 0.2,
        color = black,
        points(pts) 
   ),

  gr2d(title = " Lyapunov exponent ",
   points_joined =true,
   yaxis       = true,
   xaxis 	= true,
   xtics = tic,
   xlabel     = "c parameter",
   ylabel     = " lyapunov exponent ",
   yrange = [-4,1],
   color         = red,
   point_size    = 0.5,
   point_type = filled_circle,
   points(ls),
   points_joined =false,
   point_size    = 1.5,
   color = black,
   points(roots))

 );

